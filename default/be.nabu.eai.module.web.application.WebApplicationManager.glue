result = xml.objectify(file.read("webartifact.xml"))

tag("HTTP Web Application")

result/virtualHost = parameter(
	name: "virtualHost",
	category: "Basic",
	title: null,
	description: "The virtual host the application is available on",
	type: null,
	encrypted: false,
	optional: true,
	raw: result/virtualHost)/current

result/path = parameter(
	name: "path",
	category: "Basic",
	title: null,
	description: "The path this application is available on",
	type: null,
	encrypted: false,
	optional: true,
	raw: result/path)/current

file.write("webartifact.xml", xml.stringify(result))



fragments = xml.objectify(file.read("fragments.xml"))

for (part : fragments/parts)
	string definition = file.read("definition-" + part/type + ".xml")
	if (definition != null)
		keyValuePairs = derive(lambda(x, structure(key: x["@key"], value: x["$value"])), part/configuration/property)
		object = propertiesToObject(definition, properties: unwrap(keyValuePairs))
		# we use the fully type name as a prefix, we assume the same type is only configured once (which is generally the case)
		# we can't really use indexed access because the layout of the fragments might change
		# we could generate a unique id per fragment and use that as an alternative but that is less readable
		parameters = parameters(definition, lambda(x, object[string.substring(script.size(part/type) + 1, string: x)]), part/environmentSpecific == true, prefix: part/type)
		
		parsedDefinition = xml.objectify(definition)
		
		for (parameter : parameters)
			localName = string.substring(script.size(part/type) + 1, string: parameter/name)
			if (parameter/list)
				object[/localName] = split("[\s]*,[\s]*", parameter/current)
			else
				object[/localName] = parameter/current
			#parameter/category = replace("^.*\\.([^.]+)$", "$1", part/type)
			parameter/category = when(parsedDefinition["@comment"] != null, parsedDefinition["@comment"], part/type)
			parameter/title = localName
		
		# this is actual key/value pairs
		keyValuePairs = objectToProperties(object)
		# we want to transform it into attribute "key" and a value
		keyValuePairs = resolve(derive(lambda(x, structure(lambda("@key"): x["key"], lambda("$value"): x/value)), keyValuePairs))
		part/configuration/property = keyValuePairs
	else
		echo("Could not find definition for: " + part/type)

file.write("fragments.xml", xml.stringify(fragments))

